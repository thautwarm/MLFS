
# SMLFS programming language v0.1

%%inline
from json.decoder import py_scanstring
def parse_str(s):
    return py_scanstring(s, 1)[0]

def str_concat(a, b):
    return a + b

def mk_tuple(T, xs):
    if len(xs) == 1:
        return xs[0]
    return T(xs)

def mk():
  TForall = "TForall"
  TApp = "TApp"
  TArrow = "TArrow"
  TTup = "TTup"
  TImplicit = "TImplicit"
  TVar = "TVar"
  TSym = "TSym"
  TNew = "TNew"
  TQuery = "TQuery"

  DAnn = "DAnn"
  DLoc = "DLoc"
  DBind = "DBind"
  DQuery = "DQuery"
  DOpen = "DOpen"

  ELoc = "ELoc"
  EVar = "EVar"
  EVal = "EVal"
  ELet = "ELet"
  EITE = "EITE"
  EFun = "EFun"
  EApp = "EApp"
  ETup = "ETup"
  EExt = "EExt"
  EQuery = "EQuery"
  EField = "EField"
  ECoerce = "ECoerce"
  I64 = "I64"
  F64 = "F64"
  U64 = "U64"
  Str = "Str"
  Bl = "Bl"
  out = {}

  for e in locals():
    def mk(e=e):
      def create(*args):
        if not args:
          return {'value': e}
        if len(args) is 1:
          return {e: args[0]}
        return {e: args}
      return create
    out[e] = mk()

  def Expr(pos, typ, impl):
    return {'pos':pos,  'typ': typ, 'impl': impl}
  def Pos(token):
    return {'line': token.lineno + 1, 'col': token.colno, 'filename': token.filename}
  def ModuleRecord(name, imports, decls):
    return dict(name=name, imports=imports, decls=decls)
  out['ModuleRecord'] = ModuleRecord
  out['Expr'] = Expr
  out['Pos'] = Pos
  return out

globals().update(mk())

def tforall(a, b):
  if a:
    return TForall(a, b)
  return b

def mk_type_def(type_head, definition):
    mode, definition = definition
    if mode != 0:
      raise NotImplementedError("can only define record now")
    pos, type_name, bounds = type_head
    generated_ast = [
      DLoc(pos),
      DAnn(type_name, TApp(TVar("Type"), TNew(type_name))),
      DBind(type_name, EVar(type_name))
    ]
    record_type = TVar(type_name)

    for each in bounds:
      record_type = TApp(record_type, TVar(each))


    field_class = TVar("field")
    structure_parameter_type = TTup([f_type for (_, _, f_type) in definition])
    nominal_parameter_type = record_type

    n_fields = len(definition)
    function = ETup([EVar(f_name) for (_, f_name, _) in definition])
    eltypes = []
    for i, (f_pos, f_name, f_type) in enumerate(reversed(definition)):
      i = n_fields - i - 1
      structure_parameter_type = TArrow(f_type, structure_parameter_type)
      nominal_parameter_type = TArrow(f_type, nominal_parameter_type)
      eltypes.append(f_type)
      function = EFun(f_name, function)

      generated_ast.append(DLoc(f_pos))
      field_instance = TImplicit(
        tforall(
          bounds,
          TApp(
            field_class,
            TTup([record_type, TSym(f_name), f_type]))))

      eltypes.reverse()

      getter_name = "__get_" + f_name

      generated_ast.append(
        DAnn(getter_name, field_instance))

      generated_ast.append(
        DBind(
          getter_name,
          ECoerce(
            EApp(
              EVar("op_Element"),
              EVal(U64(i))))))

    nominal_parameter_type = tforall(bounds, nominal_parameter_type)
    structure_parameter_type = tforall(bounds, structure_parameter_type)
    make_name = "__make_" + type_name
    make_instance = TImplicit(
        TApp(
          TVar("Make"),
          TTup(
            [
              TVar(type_name),
              nominal_parameter_type
            ])))

    generated_ast.extend([
      DAnn(make_name, make_instance),
      DBind(
        make_name,
        ECoerce(
          ELet(
            [
              DAnn(make_name, structure_parameter_type),
              DBind(make_name, function),
            ],
            EVar(make_name))))
    ])
    return generated_ast

def concat(xs):
  if len(xs) == 1:
    return xs[0]
  res = []
  for x in xs:
    res.extend(x)
  return res

def importName(x):
  return x.split('.')[-1]

def inc(x):
  return x + 1
%%

list[p] : p         { [$1] }
        | list[p] p { $1.append($2); $1 }
        ;

sep_list[comma, p]
        : p                          { [$1] }
        | sep_list[comma, p] comma p { $1.append($3); $1 }
        ;

START : <BOF> toplevel <EOF> { $2 };

toplevel : 'module' nameStr
           list[import]
           list[declaration]
           { ModuleRecord($2, $3, concat($4)) }
          | 'module' nameStr
           list[declaration]
           { ModuleRecord($2, [], concat($3)) } ;

import : 'import' nameStr 'as' nameStr
          { ($2, $4) }
       | 'import' nameStr
          { ($2, importName($2)) }
        ;

tvar    : <name> { TVar($1.value) };

toptype : 'forall' list[nameStr] '.' toptype { TForall($2, $4) }
        |  arrowtype  { $1 }
        ;

arrowtype : calltype '->' toptype { TArrow($1, $3) }
          | calltype { $1 }
          ;

calltype : calltype type { TApp($1, $2) }
         | type          { $1 }
         ;

nameStr : <name> { $1.value };

type : tvar { $1 }
     | '(' ')'                        { TTup([]) }
     | '(' sep_list[',', toptype] ')' { mk_tuple(TTup, $2) }
     | '{' toptype '}' { TImplicit($2) }
     | <str> { TSym(parse_str($1.value)) }
     | '@' <name> { TNew($2.value) }
     | '?' type 'as' nameStr   { TQuery($4, $2) }
     ;


type_head :  <name> list[nameStr] { (Pos($1), $1.value, $2) }
          |  <name> { (Pos($1), $1.value, []) }
          ;

record_def : '(' sep_list[',', record_field_def] ')' { (0, $2) }
           | '(' ')' { (0, []) }
           ;

record_field_def : nameStr ':' toptype { (Pos($2), $1,  $3) };

adt_def : list[variant_def] { (1, $1) };

variant_def : '|' nameStr toptype ':' toptype { (Pos($1), $2, $3, $5) };

declaration : 'val' <name> ':' toptype
            { [DLoc(Pos($1)), DAnn($2.value, $4)] }
          | 'let' <name> '=' expr
            { [DLoc(Pos($1)), DBind($2.value, $4)] }
          | 'check' nameStr 'as' nameStr
            { [DLoc(Pos($1)), DQuery($4, $2)] }
          | 'open' expr
            { [DLoc(Pos($1)), DOpen($2)] }
          | 'type' type_head '=' (record_def | variant_def)
            { mk_type_def($2, $4) }

          ;

expr : 'if' expr 'then' expr 'else' expr
       { EITE($2, $4, $6) }
     | list[declaration] 'in' expr
       { ELet(concat($1), $3) }
     | 'fun' <name> '->' expr
       { EFun($2.value, $4) }
     | 'coerce' expr { ECoerce($2) }
     | call { $1 }
     ;

call : call atom { EApp($1, $2) }
     | atom { $1 }
     ;

atom : 'extern' <str> { EExt(parse_str($2.value))  }
     | '(' sep_list[',', expr] ')'
       { mk_tuple(ETup, $2) }
     | '(' ')' { ELoc(Pos($1), ETup([])) }
     | <int> { EVal(I64(int($1.value))) }
     | <float> { EVal(F64(float($1.value))) }
     | <name> { EVar($1.value) }
     | <str> { EVal(Str(parse_str($1.value))) }
     | 'true' { EVal(Bl(True)) }
     | 'false' { EVal(Bl(False)) }
     | atom '.' nameStr
      { EField($1, $3) }
     | '?'  atom 'as' nameStr
      { EQuery($4, $2) }
     ;
